/**
 * @file ui/tests/qml/import/tst_ImportPanel.qml
 * @brief Provides QML tests for ImportPanel behavior.
 */

pragma ComponentBehavior: Bound

import QtQuick 2.15
import QtTest 1.3
import FossRedder.Views.Import 1.0 as Import

import "../Lookup.js" as Lookup
import "../TestSupport.js" as TestSupport

TestCase {
    id: testCase
    name: "ImportPanelTests"
    when: windowShown
    width: 900
    height: 360

    property var theme: QtObject {
        property int spacingSmall: 6
        property int spacingMedium: 8
        property int formLabelWidth: 120
        property int viewActionButtonWidth: 120
        property int panelPadding: 12
        property int radius: 3
        property int borderWidthThin: 1
        property int controlHeight: 32
        property color border: "#cccccc"
        property color borderSoft: "#cccccc"
        property color surface: "#ffffff"
        property color surfaceAlt: "#f5f5f5"
        property color textPrimary: "#000000"
        property color textMuted: "#666666"
    }

    property var importState: QtObject {
        property bool hasImportWorkflow: true
        property bool importRunning: false
        property string manualPathText: ""
        property int queuedCount: 0
        property var importFiles: []
        property int browseCalls: 0
        property int commitManualCalls: 0
        function browseImportPdf() { browseCalls += 1 }
        function commitManualImportFiles() { commitManualCalls += 1 }
    }

    Component {
        id: panelComponent
        Import.ImportPanel {
            width: testCase.width
            height: testCase.height
            theme: testCase.theme
            importState: testCase.importState
        }
    }

    function findRequired(root, objectName) {
        return TestSupport.findRequired(Lookup, root, objectName)
    }

    function createPanel() {
        return createTemporaryObject(panelComponent, testCase)
    }

    function init() {
        importState.hasImportWorkflow = true
        importState.importRunning = false
        importState.manualPathText = ""
        importState.queuedCount = 0
        importState.importFiles = []
        importState.browseCalls = 0
        importState.commitManualCalls = 0
    }

    function test_IMP_P_001_manualPathEditingAndAddDelegatesToImportState() {
        const panel = createPanel()
        const field = findRequired(panel, "importManualPathField")

        field.text = "P:/imports/statement.pdf"
        field.textEdited()
        findRequired(panel, "importAddFileButton").clicked()

        compare(importState.manualPathText, "P:/imports/statement.pdf")
        compare(importState.commitManualCalls, 1)
    }

    function test_IMP_P_002_browseDelegatesToImportState() {
        const panel = createPanel()

        findRequired(panel, "importBrowseFileButton").clicked()

        compare(importState.browseCalls, 1)
    }
}
